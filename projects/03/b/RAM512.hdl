// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/b/RAM512.hdl

/**
 * Memory of 512 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    // We can reuse the logic from RAM64
    // The highest most significant bits (address [6..8]) selects the RAM64 chip
    // The lowest least significant bits (address [0..5]) selects the register within the RAM64 chip
    DMux8Way(in=load, sel=address[6..8], a=l0, b=l1, c=l2, d=l3, e=l4, f=l5, g=l6, h=l7);
    RAM64(in=in, load=l0, address=address[0..5], out=ram0out);
    RAM64(in=in, load=l1, address=address[0..5], out=ram1out);
    RAM64(in=in, load=l2, address=address[0..5], out=ram2out);
    RAM64(in=in, load=l3, address=address[0..5], out=ram3out);
    RAM64(in=in, load=l4, address=address[0..5], out=ram4out);
    RAM64(in=in, load=l5, address=address[0..5], out=ram5out);
    RAM64(in=in, load=l6, address=address[0..5], out=ram6out);
    RAM64(in=in, load=l7, address=address[0..5], out=ram7out);

    Mux8Way16(a=ram0out,
              b=ram1out,
              c=ram2out,
              d=ram3out,
              e=ram4out,
              f=ram5out,
              g=ram6out,
              h=ram7out,
              sel=address[6..8],
              out=out);
}