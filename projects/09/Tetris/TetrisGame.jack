/**
 * Implements the Tetris game.
 */

/** Class responsible for updating the model of the game */
class TetrisGame {
  field int gameSpeed;  // Speed of the game

  field Background gameBackground;  // The game background
  // Destroyed by the gameBackground
  field Mesh gameMesh;  // The game mesh

  /**
   * Construct a new Tetris Game.
   *
   * The constructor will:
   * - Draw the background frame
   * - Initialize the background
   */
  constructor TetrisGame new() {
    // Set initial game speed
    // FIXME: Make game speed work with level
    let gameSpeed = 500;

    // FIXME: YOU ARE HERE: Refactor drawing on mesh to be relative to background
    // Initialize the background (see README.md for calculation)
    // NOTE: The mesh is 40 according to
    // https://tetris.fandom.com/wiki/Tetris_Guideline#List_of_rules_(as_of_2009)
    let gameBackground = Background.new(1,   // frameStartX_
                                        6,   // frameStartY_
                                        122, // frameLenX_
                                        242, // frameLenY_ (240 + 1 for next line + 1 for frame line)
                                        10,  // tetrominoElementSize_
                                        20,  // skipRows_
                                        10,  // meshCols_
                                        40   // meshRows_
                                        );
    let gameMesh = gameBackground.getBackgroundMesh();
    do gameBackground.drawFrame();

    return this;
  }

  /** Dispose this game. */
  method void dispose() {
    do gameBackground.dispose();
    do Memory.deAlloc(this);
    return;
  }

  /**
   * Run the game by:
   *
   * 1. Prepare the tetromino sequence (7-bag draw)
   * 2. Spawning a tetromino
   * 3. Let it fall (capturing of keystrokes happens during fall)
   * 4. Check if a row is covered and remove the row
   * 5. Update the score
   * 6. Check if the game is over
   */
  method void run() {
    var bool doSpawn;  // Whether we will spawn or not
    var bool gameOver;  // Whether the game is over or not

    var Tetromino curTetromino;  // The current tetromino

    // FIXME: Remove
    var int testRow;
    var int testCol;
    let testRow = 39;

    // FIXME: TestMesh
    while(testRow > 28){
      let testCol = 0;
      while (testCol < 10){
        do gameMesh.setState(testCol, testRow, true);
        let testCol = testCol + 1;
      }
      let testRow = testRow - 1;
    }

    // Make holes
    do gameMesh.setState(0, 39, false);
    do gameMesh.setState(2, 39, false);
    do gameMesh.setState(4, 39, false);
    do gameMesh.setState(6, 39, false);
    do gameMesh.setState(8, 39, false);
    // 38 is due to clearing
    do gameMesh.setState(1, 37, false);
    do gameMesh.setState(3, 37, false);
    do gameMesh.setState(5, 37, false);
    do gameMesh.setState(7, 37, false);
    do gameMesh.setState(9, 37, false);
    // 36 - 35 is due to clearing
    do gameMesh.setState(0, 34, false);
    do gameMesh.setState(4, 34, false);
    do gameMesh.setState(5, 34, false);
    do gameMesh.setState(9, 34, false);
    // 33 - 30 is due to clearing
    do gameMesh.setState(1, 29, false);
    do gameMesh.setState(8, 29, false);

    do gameBackground.drawMesh(false, gameMesh, 0, 0);





    // Run until game over
    while (~gameOver){
      let curTetromino = TetrominoSpawner.getO(gameBackground);
      let doSpawn = curTetromino.canSpawn();
      if(doSpawn){
        // Enter the fall sequence
        do tetrominoFall(curTetromino);
      }
      // The game can end by "Top out" if a tetromino cannot spawn:
      // https://tetris.fandom.com/wiki/Top_out
      let gameOver = ~doSpawn;

      // Dispose the current tetromino
      do curTetromino.dispose();
    }

    // Display game over
    do Output.moveCursor(11, 2);
    do Output.printString(" Game over! ");

    return;
  }

  /** Let the tetromino fall and move it around while falling */
  method void tetrominoFall(Tetromino curTetromino){
    var int countDown;  // Count down until next move down
    var bool falling;  // Whether or not the tetromino is still falling
    var char key;  // Variable for capturing key stroke
    var char prevKey;  // Variable for the previous key stroke

    let countDown = gameSpeed;
    let falling = true;

    while(falling){

      // Wait for a key to be pressed
      while(((key=0) & (countDown > 0))){
        let key = Keyboard.keyPressed();

        do Sys.wait(1);
        let countDown = countDown - 1;
      }
      // If the current key is the same as the previous key it's due to the key
      // not being released before the countDown ended
      // We therefore add a guard to make sure that the key get's properly released
      if (~(key = prevKey)){ do moveWithKeyboard(curTetromino, countDown, key); }
      let prevKey = key;

      // Wait for the key to be release to continue
      // If not the key will be registered several times as the
      // content of the "falling" while loop is traversed quite fast
      while((~(key=0)) & (countDown > 0)){
        let prevKey = 0;
        let key = Keyboard.keyPressed();
        do Sys.wait(1);
        let countDown = countDown - 1;
      }

      // If the time counts out we will add the tetromino with the mesh and destroy it
      // NOTE: We cannot set countDown < 0 to check if countDown is 0
      if(countDown < 1){
        if(curTetromino.canMoveDown()){
          do curTetromino.moveDown();
          let countDown = gameSpeed;
        }
        else{
          let falling = false;
          do addTetrominoToMesh(curTetromino);
          do removeFullLines();
        }
      }
    }
    return;
  }

  /**
   * Move the tetromino with the keyboard
   *
   * \param curTetromino The current tetromino
   * \param countDown Remaining time until lock
   * \param key Current key stroke
   */
  method void moveWithKeyboard(Tetromino curTetromino, int countDown, char key){
    // Capture up arrow
    if (key = 131) {
      let countDown = 0;
      while(curTetromino.canMoveDown()){
          do curTetromino.moveDown();
      }
    }
    // Capture down arrow
    if (key = 133) {
      if(curTetromino.canMoveDown()){
        do curTetromino.moveDown();
      }
    }
    // Capture left arrow
    if (key = 130) {
      if(curTetromino.canMoveLeft()){
        do curTetromino.moveLeft();
      }
    }
    // Capture right arrow
    if (key = 132) {
      if(curTetromino.canMoveRight()){
        do curTetromino.moveRight();
      }
    }

    return;
  }

  /**
   * Add the tetromino to the mesh
   *
   * \param curTetromino The current tetromino
   */
  method void addTetrominoToMesh(Tetromino curTetromino){
    var int gameXInd;  // x-coordinate in the gameMesh
    var int gameYInd;  // y-coordinate in the gameMesh
    var int tetrominoIndex;  // Index in the tetromino mesh

    var int tetrominoMeshLength;  // Length of the tetromino mesh
    // Will be destroyed when Mesh is destroyed in Tetromino
    var Array tetrominoIndexArray;  // The array for the tetromino coordinates
    // Will be destroyed by the Tetromino
    var Mesh tetrominoMesh;  // The tetromino mesh

    let tetrominoMesh = curTetromino.getTetrominoMesh();
    let tetrominoMeshLength = tetrominoMesh.getMeshLength();

    // Add tetromino to the game mesh
    let tetrominoIndex = 0;
    while(tetrominoIndex < tetrominoMeshLength){
      let tetrominoIndexArray = tetrominoMesh.getMeshIndices(tetrominoIndex);
      let gameXInd = curTetromino.getRelativeIndX() + tetrominoIndexArray[0];
      let gameYInd = curTetromino.getRelativeIndY() + tetrominoIndexArray[1];
      do gameMesh.setState(gameXInd, gameYInd, true);
      let tetrominoIndex = tetrominoIndex + 1;
    }
    do gameBackground.drawMesh(false, gameMesh, 0, 0);

    return;
  }

  /** Remove full lines */
  method void removeFullLines(){
    var Array rowsToRemove;
    var int nRowsToRemove;
    var int rowNumber;
    var int blinkSpeed;

    let blinkSpeed = 150;

    let rowsToRemove = getRowsToRemove();
    while(rowNumber < gameBackground.getVisibleRows()){
      if(rowsToRemove[rowNumber]){
        let nRowsToRemove = nRowsToRemove + 1;
      }
      let rowNumber = rowNumber + 1;
    }

    if(nRowsToRemove > 0){
      if (nRowsToRemove > 3){
        // Let the rows blink on Tetris
        do gameBackground.drawRows(rowsToRemove, false);
        do Sys.wait(blinkSpeed);
        do gameBackground.drawRows(rowsToRemove, true);
        do Sys.wait(blinkSpeed);
        do gameBackground.drawRows(rowsToRemove, false);
        do Sys.wait(blinkSpeed);
        do gameBackground.drawRows(rowsToRemove, true);
        do Sys.wait(blinkSpeed);
      }

      do gameBackground.clearLinesAnimation(rowsToRemove);

      // Collapse full rows
      do gameBackground.collapse(rowsToRemove);
    }

    // Created in getRowsToRemove
    do rowsToRemove.dispose();
    return;
  }

  /**
   * Get rows to remove
   *
   * \warning The returned array must be manually destroyed
   */
  method Array getRowsToRemove(){
    var int xInd;  // The x coordinate of the Mesh
    var int curRow;  // The y coordinate of the Mesh

    var bool foundWhiteSpace;
    var bool state;

    var Array rowsToRemove;
    var Mesh backgroundMesh;

    // Disposed in removeFullLines
    let rowsToRemove = Array.new(gameBackground.getVisibleRows());

    let backgroundMesh = gameBackground.getBackgroundMesh();

    let curRow = gameBackground.getSkipRows();

    // Loop over all visible rows
    while(curRow < gameBackground.getMeshRows()){
      let xInd = 0;

      // Loop over all rows in a column
      // Reset foundWhiteSpace
      let foundWhiteSpace = false;
      while ((xInd < backgroundMesh.getCols()) & (~foundWhiteSpace)){
        let state = backgroundMesh.getState(xInd, curRow);
        if(state = 0){
          let foundWhiteSpace = true;
        }
        let xInd = xInd + 1;
      }

      // If we didn't find a whitespace, mark the row for removal
      // NOTE: Although we dispose the rowsToRemove, the array is not initialized to zero
      //       Hence we must  specify both rows to remove and rows to keep
      if(~foundWhiteSpace){
        let rowsToRemove[curRow - gameBackground.getSkipRows()] = true;
      } else {
        let rowsToRemove[curRow - gameBackground.getSkipRows()] = false;
      }

      let curRow = curRow + 1;
    }

    return rowsToRemove;
  }

}
