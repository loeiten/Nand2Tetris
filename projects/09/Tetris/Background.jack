/**
 * Implements the Background class
 */

/**
 * The background is the location where all inactive tetrominos are drawn
 */
class Background {
  field int frameStartX_;  // The pixel column for the start of the frame
  field int frameStartY_;  // The pixel row for the start of the frame
  field int tetrominoStartX_;  // The pixel column for the start of the tetromino
  field int tetrominoStartY_;  // The pixel row for the start of the tetromino
  field int frameLenX_;  // The length of the frame in the x direction
  field int frameLenY_;  // The length of the frame in the y direction
  field int tetrominoElementSize_;  // The size of a tetromino element
  field int skipRows_;  // Number of invisible rows (counting from the top)
  field int meshCols_;  // Number of columns in the mesh
  field int meshRows_;  // Number of rows in the mesh
  field int visibleRows;  // Number of visible rows

  field Mesh backgroundMesh;  // Mesh holding the state of each element

  static int allocs;  // Number of times this has been allocated

  /**
   * Construct the Background
   *
   * \param frameStartX The pixel column for the start of the background
   * \param frameStartY The pixel row for the start of the background
   * \param frameLenX The length of the frame in the x direction
   * \param frameLenY The length of the frame in the y direction
   * \param tetrominoElementSize The size of a tetromino element
   * \param skipRows Number of invisible rows (counting from the top)
   * \param meshCols Number of columns for the backgroundMesh
   * \param meshRows Number of rows for the backgroundMesh
   */
  constructor Background new(int frameStartX,
                             int frameStartY,
                             int frameLenX,
                             int frameLenY,
                             int tetrominoElementSize,
                             int skipRows,
                             int meshCols,
                             int meshRows) {
    let frameStartX_ = frameStartX;
    let frameStartY_ = frameStartY;

    // The start of the tetromino is +1 padding and +1 for the line
    let tetrominoStartX_ = frameStartX_ + 2;
    let tetrominoStartY_ = frameStartY_ + 2;

    let frameLenX_ = frameLenX;
    let frameLenY_ = frameLenY;

    let tetrominoElementSize_ = tetrominoElementSize;

    let meshCols_ = meshCols;
    let meshRows_ = meshRows;
    let skipRows_ = skipRows;
    let visibleRows = meshRows_ - skipRows_;

    let backgroundMesh = Mesh.new(meshCols_, meshRows_);

    let allocs = allocs + 1;

    return this;
  }

  /**
   * Destroy the Background
   *
   * Disposes:
   * - The backgroundMesh
   * - This
   */
  method void dispose (){
    let allocs = allocs - 1;
    do backgroundMesh.dispose();
    do Memory.deAlloc(this);
    return;
  }

  /** Print the number of allocs */
  method void printAlloc (){
    var String str;
    let str = "Background allocs: ";
    do Output.printString(str);
    do Output.printInt(allocs);
    do str.dispose();
    return;
  }

  /** Return the background Mesh */
  method Mesh getBackgroundMesh(){
    return backgroundMesh;
  }

  /** Return the number of rows in the Mesh*/
  method int getMeshRows(){
    return meshRows_;
  }

  /** Return the skip rows */
  method int getSkipRows(){
    return skipRows_;
  }

  /** Return the visible rows */
  method int getVisibleRows(){
    return visibleRows;
  }

  /**
   * Collapse full rows
   *
   * \param rowsToCollapse Rows to collapse
   */
  method void collapse(Array rowsToCollapse){
    // We will use the following strategy:
    // 1. Iterate from the bottom row
    // 2. When a rowToRemove is encountered:
    //    a. Iterate from the bottom to the top:
    //       - If a element has state = true: Set the state of the element below to 0

    var int xInd;  // The x coordinate of the Mesh
    var int potentialCollapseRow;  // The y coordinate of the row to potentially collapse
    var int meshRow;  // The y coordinate of the mesh

    var int clearedRows;

    var bool state;

    // NOTE: meshRows_ counts the total number of rows
    //       potentialCollapseRow will be used as a index (starting at 0)
    //       Hence we need to subtract by one
    let potentialCollapseRow = meshRows_ - 1;

    while(potentialCollapseRow > skipRows_){
      if(rowsToCollapse[potentialCollapseRow - skipRows_]){
        // Start at the row immediately above the collapsed row
        // Also take into account previously cleared rows
        let meshRow = potentialCollapseRow - 1 + clearedRows;
        while(meshRow > skipRows_){
          let xInd = 0;

          // Loop over all rows
          while(xInd < meshCols_){
            // Set the element of the row to be cleared to the row above
            let state = backgroundMesh.getState(meshRow, xInd);
            do backgroundMesh.setState(meshRow + 1, xInd, state);
            let xInd = xInd+1;
          }
          let meshRow = meshRow - 1;
        }
        let clearedRows = clearedRows + 1;
      }
      let potentialCollapseRow = potentialCollapseRow - 1;
    }

    // Draw the updated background
    // Remove all elements
    do drawMesh(true, true, backgroundMesh, 0, 0);
    // Draw the elements
    do drawMesh(false, false, backgroundMesh, 0, 0);
    return;
  }

  /** Draw the frame */
  method void drawFrame(){
    // Draw background frame
    do Screen.setColor(true);
    do Screen.drawLine(frameStartX_, frameStartY_, frameStartX_ + frameLenX_, frameStartY_);
    do Screen.drawLine(frameStartX_, frameStartY_, frameStartX_, frameStartY_ + frameLenY_);
    do Screen.drawLine(frameStartX_ + frameLenX_, frameStartY_, frameStartX_ + frameLenX_, frameStartY_ + frameLenY_);
    do Screen.drawLine(frameStartX_, frameStartY_ + frameLenY_, frameStartX_ + frameLenX_, frameStartY_ + frameLenY_);

    return;
  }

  /**
   * Draw a mesh on the background
   *
   * \param eraseMode Erase tetrominos (depending on eraseAll)
   * \param eraseAll Only effective if eraseMode is true
   *                 - If true: All elements will be erased
   *                 - If false: Only elements with state true will be erased
   * \param mesh The Mesh to draw on the background
   * \param relativeYInd The y index of the input mesh relative to the background (counted from the top left)
   * \param relativeXInd The x index of the input mesh relative to the background (counted from the top left)
   */
  method void drawMesh(bool eraseMode,
                       bool eraseAll,
                       Mesh mesh,
                       int relativeYInd,
                       int relativeXInd){
    var int backgroundIndex;  // Index in the background mesh
    var int backgroundXInd;  // The x coordinate of the background mesh
    var int backgroundYInd;  // The y coordinate of the background mesh
    var int meshXInd;  // The x coordinate of the input mesh
    var int meshYInd;  // The y coordinate of the input mesh
    var int startPixelX;  // The pixel column to start draw a rectangle from
    var int startPixelY;  // The pixel row to start draw a rectangle from

    var bool state;  // State of the current input mesh element

    // Will be destroyed by the Mesh
    var Array backgroundCoordinates;  // Array holding the (x, y) coordinate of the background mesh

    let backgroundIndex = skipRows_ * meshCols_;

    while (backgroundIndex < backgroundMesh.getMeshLength()){
      // Get the background coordinates
      let backgroundCoordinates = backgroundMesh.getMeshIndices(backgroundIndex);
      let backgroundXInd = backgroundCoordinates[0];
      let backgroundYInd = backgroundCoordinates[1];

      // Get the input mesh coordinates
      let meshXInd = backgroundXInd - relativeXInd;
      let meshYInd = backgroundYInd - relativeYInd;

      // Only proceed if meshXInd and meshYInd is within the range
      if (
        // Check the x coordinate
        ((-1 < meshXInd) & (meshXInd < mesh.getCols())) &
        // Check the y coordinate
        ((-1 < meshYInd) & (meshYInd < mesh.getRows()))
      ){
        // NOTE: We need the actual y-coordinate to get the state
        let state = mesh.getState(meshYInd, meshXInd);
        // NOTE: Unless we are erasing everything, we only draw the active part
        //       of the mesh as the inactive part may overlap with the background
        if(state | (eraseMode & eraseAll)){
          // Subtract rows to skip, so that we only draw the visible part of the mesh
          let backgroundYInd = backgroundYInd - skipRows_;
          let startPixelX = tetrominoStartX_ + (tetrominoElementSize_*backgroundXInd) + (2*backgroundXInd);
          let startPixelY = tetrominoStartY_ + (tetrominoElementSize_*backgroundYInd) + (2*backgroundYInd);

          if(eraseMode){
            do Screen.setColor(false);
          } else {
            do Screen.setColor(state);
          }
          do Screen.drawRectangle(startPixelX,
                                  startPixelY,
                                  startPixelX+tetrominoElementSize_,
                                  startPixelY+tetrominoElementSize_);
        }

      }

      let backgroundIndex = backgroundIndex + 1;
    }

    return;
  }

  /**
   * Draw specific rows with a certain color
   *
   * \param rowsToDraw Rows to draw with the color
   * \param color The color to draw
   */
  method void drawRows(Array rowsToDraw, bool color){
    var int xInd;  // The x coordinate of the Mesh
    var int curRow;  // The y coordinate of the Mesh
    var int visibleRow;  // The y coordinate of visible row
    var bool drawRow;  // Whether or not to draw the row
    var int startPixelX;  // The pixel column to start draw a rectangle from
    var int startPixelY;  // The pixel row to start draw a rectangle from

    let curRow = skipRows_;

    do Screen.setColor(color);

    // Loop over all visible rows
    while(curRow < meshRows_){
      let visibleRow = curRow - skipRows_;
      let drawRow = rowsToDraw[visibleRow];

      if(drawRow){
        let xInd = 0;

        // Loop over all rows in a column
        while (xInd < meshCols_){
          let startPixelX = tetrominoStartX_ + (tetrominoElementSize_*xInd) + (2*xInd);
          let startPixelY = tetrominoStartY_ + (tetrominoElementSize_*visibleRow) + (2*visibleRow);
          do Screen.drawRectangle(startPixelX, startPixelY, startPixelX+tetrominoElementSize_, startPixelY+tetrominoElementSize_);
          let xInd = xInd + 1;
        }
      }
      let curRow = curRow + 1;
    }

    return;
  }

  /**
   * Animate clear lines
   *
   * \param rowsToClear Rows to draw with the color
   */
  method void clearLinesAnimation(Array rowsToClear){
    var int curRow;  // The y coordinate of the Mesh
    var int visibleRow;  // The y coordinate of visible row
    var bool clearRow;  // Whether or not to draw the row
    var int startPixelX;  // The pixel column to start draw a rectangle from
    var int startPixelY;  // The pixel row to start draw a rectangle from

    var int xIndHalf;
    var int xIndRight;
    var int xIndLeft;
    var int xIndRightCounter;
    var int xIndLeftCounter;

    let xIndHalf = (meshCols_/2);
    let xIndLeftCounter = 1;
    let xIndRight = xIndHalf + xIndRightCounter;
    let xIndLeft = xIndRight - xIndLeftCounter;

    do Screen.setColor(false);

    // Loop over half the xInds
    while(xIndRight < meshCols_){

      // Loop over all visible rows
      let curRow = skipRows_;
      while(curRow < meshRows_){
        let visibleRow = curRow - skipRows_;
        let clearRow = rowsToClear[visibleRow];

        if(clearRow){
          // Remove the right element
          let startPixelX = tetrominoStartX_ + (tetrominoElementSize_*xIndRight) + (2*xIndRight);
          let startPixelY = tetrominoStartY_ + (tetrominoElementSize_*visibleRow) + (2*visibleRow);
          do Screen.drawRectangle(startPixelX, startPixelY, startPixelX+tetrominoElementSize_, startPixelY+tetrominoElementSize_);
          // Remove the left element
          let startPixelX = tetrominoStartX_ + (tetrominoElementSize_*xIndLeft) + (2*xIndLeft);
          let startPixelY = tetrominoStartY_ + (tetrominoElementSize_*visibleRow) + (2*visibleRow);
          do Screen.drawRectangle(startPixelX, startPixelY, startPixelX+tetrominoElementSize_, startPixelY+tetrominoElementSize_);
        }

        let curRow = curRow + 1;
      }

      let xIndLeftCounter = xIndLeftCounter + 2;
      let xIndRightCounter = xIndRightCounter + 1;

      let xIndRight = xIndHalf + xIndRightCounter;
      let xIndLeft = xIndRight - xIndLeftCounter;

      do Sys.wait(100);
    }

    return;
  }

}
