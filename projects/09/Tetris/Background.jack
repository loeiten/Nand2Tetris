/**
 * Implements the Background class
 */

/**
 * The background is the location where all inactive tetrominos are drawn
 */
class Background {
  field int frameStartX;  // The pixel column for the start of the background
  field int frameStartY;  // The pixel row for the start of the background
  field int meshElementSize;  // The size of a tetromino element
  field int skipRows;  // Number of invisible rows (counting from the top)
  field int meshCols;  // Number of columns in the mesh
  field int meshRows;  // Number of rows in the mesh
  field int visibleRows;  // Number of visible rows

  field Mesh backgroundMesh;  // Mesh holding the state of each element

  /**
   * Construct the Background
   *
   * \param frameStartX_ The pixel column for the start of the background
   * \param frameStartY_ The pixel row for the start of the background
   * \param meshElementSize_ The size of a tetromino element
   * \param skipRows_ Number of invisible rows (counting from the top)
   * \param meshCols_ Number of columns for the backgroundMesh
   * \param meshRows_ Number of rows for the backgroundMesh
   */
  constructor Background new(int frameStartX_,
                             int frameStartY_,
                             int meshElementSize_,
                             int skipRows_,
                             int meshCols_,
                             int meshRows_) {
    let frameStartX = frameStartX_;
    let frameStartY = frameStartY_;
    let meshElementSize = meshElementSize_;

    let meshCols = meshCols_;
    let meshRows = meshRows_;
    let skipRows = skipRows_;
    let visibleRows = meshRows - skipRows;

    let backgroundMesh = Mesh.new(meshCols, meshRows);

    return this;
  }

  /**
   * Destroy the Background
   *
   * Disposes:
   * - The backgroundMesh
   * - This
   */
  method void dispose (){
    do backgroundMesh.dispose();
    do Memory.deAlloc(this);
    return;
  }

  /** Return the background Mesh */
  method Mesh getBackgroundMesh(){
    return backgroundMesh;
  }

  /** Return the number of rows in the Mesh*/
  method int getMeshRows(){
    return meshRows;
  }

  /** Return the skip rows */
  method int getSkipRows(){
    return skipRows;
  }

  /** Return the visible rows */
  method int getVisibleRows(){
    return visibleRows;
  }

  /** Draw the background */
  method void draw(){
    var int index;  // Index representing the Mesh element
    var int xInd;  // The x coordinate of the Mesh
    var int yInd;  // The y coordinate of the Mesh
    var int xStartPixel;  // The pixel column to start draw a rectangle from
    var int yStartPixel;  // The pixel row to start draw a rectangle from

    var bool state;  // State of the current Mesh element

    // Will be destroyed by the Mesh
    var Array coordinates;  // Array holding the (x, y) coordinate of the Mesh

    let index = skipRows * meshCols;

    while (index < backgroundMesh.getMeshLength()){
      let coordinates = backgroundMesh.getMeshIndices(index);
      let xInd = coordinates[0];
      let yInd = coordinates[1];
      // NOTE: We need the actual y-coordinate to get the state
      let state = backgroundMesh.getState(xInd, yInd);

      // NOTE: Since both our abstract Mesh and our display start counting rows
      //       from 0, we must subtract skipRows in order not to draw the
      //       rectangle out of range
      let yInd = yInd - skipRows;
      let xStartPixel = frameStartX + (meshElementSize*xInd) + (2*xInd);
      let yStartPixel = frameStartY + (meshElementSize*yInd) + (2*yInd);

      do Screen.setColor(state);
      do Screen.drawRectangle(xStartPixel, yStartPixel, xStartPixel+meshElementSize, yStartPixel+meshElementSize);

      let index = index + 1;
    }

    return;
  }

  /**
   * Draw specific rows with a certain color
   *
   * \param rowsToDraw Rows to draw with the color
   * \param color The color to draw
   */
  method void drawRows(Array rowsToDraw, bool color){
    var int xInd;  // The x coordinate of the Mesh
    var int curRow;  // The y coordinate of the Mesh
    var int visibleRow;  // The y coordinate of visible row
    var bool drawRow;  // Whether or not to draw the row
    var int xStartPixel;  // The pixel column to start draw a rectangle from
    var int yStartPixel;  // The pixel row to start draw a rectangle from

    let curRow = skipRows;

    do Screen.setColor(color);

    // Loop over all visible rows
    while(curRow < meshRows){
      let visibleRow = curRow - skipRows;
      let drawRow = rowsToDraw[visibleRow];

      if(drawRow){
        let xInd = 0;

        // Loop over all rows in a column
        while (xInd < meshCols){
          let xStartPixel = frameStartX + (meshElementSize*xInd) + (2*xInd);
          let yStartPixel = frameStartY + (meshElementSize*visibleRow) + (2*visibleRow);
          do Screen.drawRectangle(xStartPixel, yStartPixel, xStartPixel+meshElementSize, yStartPixel+meshElementSize);
          let xInd = xInd + 1;
        }
      }
      let curRow = curRow + 1;
    }

    return;
  }

  /**
   * Animate clear lines
   *
   * \param rowsToClear Rows to draw with the color
   */
  method void clearLinesAnimation(Array rowsToClear){
    var int curRow;  // The y coordinate of the Mesh
    var int visibleRow;  // The y coordinate of visible row
    var bool clearRow;  // Whether or not to draw the row
    var int xStartPixel;  // The pixel column to start draw a rectangle from
    var int yStartPixel;  // The pixel row to start draw a rectangle from

    var int xIndHalf;
    var int xIndRight;
    var int xIndLeft;
    var int xIndRightCounter;
    var int xIndLeftCounter;

    let xIndHalf = (meshCols/2);
    let xIndLeftCounter = 1;
    let xIndRight = xIndHalf + xIndRightCounter;
    let xIndLeft = xIndRight - xIndLeftCounter;

    do Screen.setColor(false);

    // Loop over half the xInds
    while(xIndRight < meshCols){

      // Loop over all visible rows
      let curRow = skipRows;
      while(curRow < meshRows){
        let visibleRow = curRow - skipRows;
        let clearRow = rowsToClear[visibleRow];

        if(clearRow){
          // Remove the right element
          let xStartPixel = frameStartX + (meshElementSize*xIndRight) + (2*xIndRight);
          let yStartPixel = frameStartY + (meshElementSize*visibleRow) + (2*visibleRow);
          do Screen.drawRectangle(xStartPixel, yStartPixel, xStartPixel+meshElementSize, yStartPixel+meshElementSize);
          // Remove the left element
          let xStartPixel = frameStartX + (meshElementSize*xIndLeft) + (2*xIndLeft);
          let yStartPixel = frameStartY + (meshElementSize*visibleRow) + (2*visibleRow);
          do Screen.drawRectangle(xStartPixel, yStartPixel, xStartPixel+meshElementSize, yStartPixel+meshElementSize);
        }

        let curRow = curRow + 1;
      }

      let xIndLeftCounter = xIndLeftCounter + 2;
      let xIndRightCounter = xIndRightCounter + 1;

      let xIndRight = xIndHalf + xIndRightCounter;
      let xIndLeft = xIndRight - xIndLeftCounter;

      do Sys.wait(100);
    }

    return;
  }

  /**
   * Collapse full rows
   *
   * \param rowsToCollapse Rows to collapse
   */
  method void collapse(Array rowsToCollapse){
    // We will use the following strategy:
    // 1. Iterate from the bottom row
    // 2. When a rowToRemove is encountered:
    //    a. Iterate from the bottom to the top:
    //       - If a element has state = true: Set the state of the element below to 0

    var int xInd;  // The x coordinate of the Mesh
    var int potentialCollapseRow;  // The y coordinate of the row to potentially collapse
    var int meshRow;  // The y coordinate of the mesh

    let potentialCollapseRow = meshRows;
    while(potentialCollapseRow > skipRows){

      if(rowsToCollapse[potentialCollapseRow - skipRows]){
        // Start at the row immediately above the collapsed row
        let meshRow = potentialCollapseRow + 1;
        while(meshRow > skipRows){
          let xInd = 0;

          // Loop over all rows
          while(xInd < meshRows){
            if(backgroundMesh.getState(xInd, meshRow)){
              // Set the element below to true
              do backgroundMesh.setState(xInd, meshRow - 1, true);
              // Set the current element to false
              do backgroundMesh.setState(xInd, meshRow, false);
            }
            let xInd = xInd+1;
          }

          let meshRow = meshRow - 1;
        }

      }

      let potentialCollapseRow = potentialCollapseRow - 1;
    }

    // Draw the updated background
    do draw();
    return;
  }

}
