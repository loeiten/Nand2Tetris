/**
 * Implements the Mesh class
 */

/**
 * Class representing a mesh or a grid where elements have either state 0 or 1
 */
class Mesh {
  field int cols;  // Number of cols in the Mesh
  field int rows;  // Number of rows in the Mesh
  field int meshLength;  // Total number of elements in the Mesh

  field Array meshArray;  // Array holding the state of each element
  field Array coordinates;  // Array holding the coordinates

  static int allocs;  // Number of times this has been allocated

  /**
   * Construct the mesh
   *
   * \param cols_ Number of columns in the Mesh
   * \param rows_ Number of rows in the Mesh
   */
  constructor Mesh new(int cols_, int rows_) {
    let cols = cols_;
    let rows = rows_;
    let meshLength = cols * rows;

    let meshArray = Array.new(meshLength);
    let coordinates = Array.new(2);

    let allocs = allocs + 1;

    return this;
  }

  /**
   * Destroy the Mesh
   *
   * Disposes:
   * - The meshArray
   * - The coordinates
   * - This
   */
  method void dispose (){
    let allocs = allocs - 1;
    do meshArray.dispose();
    do coordinates.dispose();
    do Memory.deAlloc(this);
    return;
  }

  /** Print the number of allocs */
  method void printAlloc (){
    var String str;
    let str = "Mesh allocs: ";
    do Output.printString(str);
    do Output.printInt(allocs);
    do str.dispose();
    return;
  }

  /**
   * Return the state at given x and y coordinates
   *
   * \param yInd The y coordinate of the Mesh
   * \param xInd The x coordinate of the Mesh
   * \return The state at the given x and y coordinates
   */
  method bool getState(int yInd, int xInd){
    var int index;
    var bool state;

    let index = getMeshArrayIndex(yInd, xInd);
    let state = meshArray[index];

    return state;
  }

  /**
   * Set the state at given x and y coordinates
   *
   * \param yInd The y coordinate of the Mesh
   * \param xInd The x coordinate of the Mesh
   * \param state The state to set
   */
  method void setState(int yInd, int xInd, bool state){
    var int index;

    let index = getMeshArrayIndex(yInd, xInd);
    let meshArray[index] = state;
    return;
  }

  /**
   * Translates a set of x and y coordinates into an index
   *
   * \param yInd The y coordinate of the Mesh
   * \param xInd The x coordinate of the Mesh
   * \return The corresponding index
   */
  method int getMeshArrayIndex(int yInd, int xInd){
    return xInd + (yInd*cols);
  }

  /**
   * Translates an index to a set of x and y coordinates
   *
   * \param index The index to translate to x and y coordinates
   * \return An array where:
   *  - The first index is the x coordinate of the Mesh
   *  - The second index is the y coordinate of the Mesh
   */
  method Array getMeshIndices(int index){
    // x-index (integer division)
    let coordinates[0] = Algorithm.mod(index, cols);

    // y-index (modolus operator)
    let coordinates[1] = index/cols;

    return coordinates;
  }

  /** Return the total number of elements in the Mesh */
  method int getMeshLength(){
    return meshLength;
  }

  /** Return the number columns in the mesh */
  method int getCols(){
    return cols;
  }

  /** Return the number rows in the mesh */
  method int getRows(){
    return rows;
  }

}
