/**
 * Implements the Tetromino class
 */

/**
 * Class which deals with the tetrominos in the following way
 * - Movement (side-ways, down, rotate)
 * - Checks if it can be placed in another mesh without colliding with elements
 *   or be out of bounds
 * - Draw and erase the tetromino within another mesh
 */
class Tetromino {
  field int relativeXInd_;  // x-coordinate in the background mesh (i.e. not the tetromino mesh coordinate) relative to top left corner of relativeMesh
  field int relativeYInd_;  // y-coordinate in the background mesh (i.e. not the tetromino mesh coordinate) relative to top left corner of relativeMesh

  field Mesh tetrominoMesh_;  // The mesh of the tetromino itself
  field Background background_;  // The background to interact with

  field int tetrominoType;  // Type of the tetromino
  field int rotationState;  // The rotation state 0 is the initial, and 1-3 is the number of rotations

  static int allocs;  // Number of times this has been allocated

  /**
   * Construct the tetromino
   *
   * \param tetrominoMesh The mesh of the tetromino itself
   * \param relativeYInd y-coordinate in the background mesh (i.e. not the tetromino mesh coordinate) relative to top left corner of relativeMesh
   * \param relativeXInd x-coordinate in the background mesh (i.e. not the tetromino mesh coordinate) relative to top left corner of relativeMesh
   * \param background The background to interact with
   * \param int The type of tetromino (0-I, 1-J, 2-L, 3-O, 4-S, 5-T, 6-Z)
   */
  constructor Tetromino new(Mesh tetrominoMesh,
                            int relativeYInd,
                            int relativeXInd,
                            Background background,
                            int type) {
    let tetrominoMesh_ = tetrominoMesh;

    let relativeXInd_ = relativeXInd;
    let relativeYInd_ = relativeYInd;

    let background_ = background;

    let tetrominoType = type;
    let rotationState = 0;

    let allocs = allocs + 1;

    return this;
  }

  /**
   * Destroy the Tetromino
   *
   * Disposes:
   * - The tetrominoMesh
   * - This
   */
  method void dispose (){
    let allocs = allocs - 1;

    do tetrominoMesh_.dispose();
    do Memory.deAlloc(this);
    return;
  }

  /** Print the number of allocs */
  method void printAlloc (){
    var String str;
    let str = "Tetromino allocs: ";
    do Output.printString(str);
    do Output.printInt(allocs);
    do str.dispose();
    return;
  }

  /** Return the tetrominoMesh_ */
  method int getTetrominoMesh(){
    return tetrominoMesh_;
  }

  /** Return relativeXInd_ */
  method int getRelativeIndX(){
    return relativeXInd_;
  }

  /** Return relativeYInd_ */
  method int getRelativeIndY(){
    return relativeYInd_;
  }
  /**
   * Return the tetromino type
   *
   * 0-I, 1-J, 2-L, 3-O, 4-S, 5-T, 6-Z
   */
  method int getTetrominoType(){
    return tetrominoType;
  }

  /**
   * Set the background
   *
   * \param background The new background
   */
  method void setBackground(Background background){
    let background_ = background;
    return;
  }

  /**
   * Set the relative position
   *
   * \param yInd The relative y index
   * \param xInd The relative x index
   */
  method void setRelativePosition(int yInd, int xInd){
    let relativeYInd_ = yInd;
    let relativeXInd_ = xInd;
    return;
  }

  /**
   * Rotate the tetromino clockwise
   */
  method void rotate(){
    var int cols;
    var int rows;
    var int curCol;
    var int curRow;
    var int originalRelX;
    var int originalRelY;
    var bool curState;
    var Mesh newMesh;
    var Array tetrominoIndices;  // Will be destroyed by the Mesh
    var Mesh backgroundMesh;

    let originalRelX = relativeXInd_;
    let originalRelY = relativeYInd_;

    let cols = tetrominoMesh_.getCols();
    let rows = tetrominoMesh_.getRows();
    let newMesh = Mesh.new(cols, rows);

    // FIXME:
    // do Output.moveCursor(0, 0);
    // do Output.printString("(curRow, curCol): (");
    // do Output.moveCursor(0, 21);
    // do Output.printString(", ");
    // do Output.moveCursor(0, 23);
    // do Output.printString(")");

    while(curCol < cols){
      let curRow = 0;
      while(curRow < rows){
        // // FIXME:
        // do Output.moveCursor(0, 20);
        // do Output.printInt(curRow);
        // do Output.moveCursor(0, 22);
        // do Output.printInt(curCol);
        // do Sys.wait(1000);

        let curState = tetrominoMesh_.getState(curRow, curCol);
        do newMesh.setState(curCol, ((rows - 1) - curRow), curState);
        let curRow = curRow + 1;
      }
      let curCol = curCol + 1;
    }

    // See if we should do a wall kick or floor kick
    if(canBePlaced(relativeYInd_, relativeXInd_, newMesh)){
      // Erase the current mesh from the background
      do background_.drawMesh(true, false, tetrominoMesh_, relativeYInd_, relativeXInd_);
      // Dispose the old mesh
      do tetrominoMesh_.dispose();
      // Set the new mesh
      let tetrominoMesh_ = newMesh;
      do background_.drawMesh(false, false, tetrominoMesh_, relativeYInd_, relativeXInd_);

      // Update the rotation state
      let rotationState = Algorithm.mod((rotationState + 1), 4);
    } else {
      // NOTE: The wall-kick changes relativeXInd_ and relativeYInd_
      if(canWallKick(newMesh)){
        // Erase the current mesh from the background
        do background_.drawMesh(true, false, tetrominoMesh_, originalRelY, originalRelX);
        // Dispose the old mesh
        do tetrominoMesh_.dispose();
        // Set the new mesh
        let tetrominoMesh_ = newMesh;
        do background_.drawMesh(false, false, tetrominoMesh_, relativeYInd_, relativeXInd_);

        // Update the rotation state
        let rotationState = Algorithm.mod((rotationState + 1), 4);
      } else {
        // Dispose the new mesh
        do newMesh.dispose();
      }
    }

    return;
  }

  method bool canWallKick(Mesh newMesh){
    // 0-I, 1-J, 2-L, 3-O, 4-S, 5-T, 6-Z
    // FIXME: Double check table
    if(tetrominoType = 0){
      // Test 2
      if(rotationState = 0){ if(tryKick(0, -2, newMesh)){ return true; } }
      if(rotationState = 1){ if(tryKick(0, 1, newMesh)){ return true; } }
      if(rotationState = 2){ if(tryKick(0, 2, newMesh)){ return true; } }
      if(rotationState = 3){ if(tryKick(0, 1, newMesh)){ return true; } }
      // Test 3
      if(rotationState = 0){ if(tryKick(0, 1, newMesh)){ return true; } }
      if(rotationState = 1){ if(tryKick(0, -2, newMesh)){ return true; } }
      if(rotationState = 2){ if(tryKick(0, -1, newMesh)){ return true; } }
      if(rotationState = 3){ if(tryKick(0, -2, newMesh)){ return true; } }
      // Test 4
      if(rotationState = 0){ if(tryKick(-1, -2, newMesh)){ return true; } }
      if(rotationState = 1){ if(tryKick(-2, 1, newMesh)){ return true; } }
      if(rotationState = 2){ if(tryKick(1, 2, newMesh)){ return true; } }
      if(rotationState = 3){ if(tryKick(-2, 1, newMesh)){ return true; } }
      // Test 5
      if(rotationState = 0){ if(tryKick(2, 1, newMesh)){ return true; } }
      if(rotationState = 1){ if(tryKick(1, -2, newMesh)){ return true; } }
      if(rotationState = 2){ if(tryKick(-2, -1, newMesh)){ return true; } }
      if(rotationState = 3){ if(tryKick(1, -2, newMesh)){ return true; } }
    } else {
      // Test 2
      if(rotationState = 0){ if(tryKick(0, -1, newMesh)){ return true; } }
      if(rotationState = 1){ if(tryKick(0, 1, newMesh)){ return true; } }
      if(rotationState = 2){ if(tryKick(0, 1, newMesh)){ return true; } }
      if(rotationState = 3){ if(tryKick(0, -1, newMesh)){ return true; } }
      // Test 3
      if(rotationState = 0){ if(tryKick(1, -1, newMesh)){ return true; } }
      if(rotationState = 1){ if(tryKick(-1, 1, newMesh)){ return true; } }
      if(rotationState = 2){ if(tryKick(1, 1, newMesh)){ return true; } }
      if(rotationState = 3){ if(tryKick(-1, -1, newMesh)){ return true; } }
      // Test 4
      if(rotationState = 0){ if(tryKick(-2, 0, newMesh)){ return true; } }
      if(rotationState = 1){ if(tryKick(2, 0, newMesh)){ return true; } }
      if(rotationState = 2){ if(tryKick(-2, 0, newMesh)){ return true; } }
      if(rotationState = 3){ if(tryKick(2, 0, newMesh)){ return true; } }
      // Test 5
      if(rotationState = 0){ if(tryKick(-2, -1, newMesh)){ return true; } }
      if(rotationState = 1){ if(tryKick(2, 1, newMesh)){ return true; } }
      if(rotationState = 2){ if(tryKick(-2, 1, newMesh)){ return true; } }
      if(rotationState = 3){ if(tryKick(2, -1, newMesh)){ return true; } }
    }

    return false;
  }

  method bool tryKick(int kickY, int kickX, Mesh newMesh){
    var int newRelativeXInd;
    var int newRelativeYInd;

    let newRelativeXInd = relativeXInd_ + kickX;
    let newRelativeYInd = relativeYInd_ + kickY;
    if(canBePlaced(newRelativeYInd, newRelativeXInd, newMesh)){
      let relativeXInd_ = newRelativeXInd;
      let relativeYInd_ = newRelativeYInd;
      return true;
    }
    return false;
  }

  /**
   * Return true if it's possible to spawn the tetromino in the backgroundMesh
   *
   * \return true if it's possible to spawn the tetromino
   */
  method bool canSpawn(){
    var bool canBeSpawn;

    let canBeSpawn = canBePlaced(relativeYInd_, relativeXInd_, tetrominoMesh_);

    if(canBeSpawn){
      do background_.drawMesh(false, false, tetrominoMesh_, relativeYInd_, relativeXInd_);
      return true;
    } else{
      return false;
    }
  }

  /**
   * Return true if it's possible to place the tetromino with the new coordinates in the backgroundMesh
   *
   * \param newRelativeYInd The new y-coordinate in the backgroundMesh
   * \param newRelativeXInd The new x-coordinate in the backgroundMesh
   * \param tetrominoMesh The mesh to try to place with
   * \return true if it's possible to place the tetromino
   */
  method bool canBePlaced(int newRelativeYInd, int newRelativeXInd, Mesh tetrominoMesh){
    var int tetrominoMeshIndex;
    var int tetrominoXIndex;
    var int tetrominoYIndex;
    var int backgroundXInd;
    var int backgroundYInd;

    var Array tetrominoIndices;  // Will be destroyed by the Mesh
    var Mesh backgroundMesh;

    let backgroundMesh = background_.getBackgroundMesh();

    while(tetrominoMeshIndex < tetrominoMesh.getMeshLength()){
      let tetrominoIndices = tetrominoMesh.getMeshIndices(tetrominoMeshIndex);
      let tetrominoXIndex = tetrominoIndices[0];
      let tetrominoYIndex = tetrominoIndices[1];

      let backgroundXInd = newRelativeXInd + tetrominoXIndex;
      let backgroundYInd = newRelativeYInd + tetrominoYIndex;

      if(tetrominoMesh.getState(tetrominoYIndex, tetrominoXIndex)){
        // Check that x is within limits
        if (backgroundXInd < 0){ return false; }
        // -1 as backgroundXInd counts from 0
        if (backgroundXInd > (backgroundMesh.getCols() - 1)){ return false; }

        // Check that y is within limits
        if (backgroundYInd < 0){ return false; }
        // -1 as backgroundYInd counts from 0
        if (backgroundYInd > (backgroundMesh.getRows() - 1)){ return false; }

        // Check whether we would collide
        if (backgroundMesh.getState(backgroundYInd, backgroundXInd)){ return false; }
      }

      let tetrominoMeshIndex = tetrominoMeshIndex + 1;
    }

    return true;
  }

  /**
   * Return true if it's possible to move the tetromino down in the backgroundMesh
   *
   * \return true if it's possible to move the tetromino down
   */
  method bool canMoveDown(){
    var int newRelativeYInd;
    var bool canPlace;

    let newRelativeYInd = relativeYInd_ + 1;
    let canPlace = canBePlaced(newRelativeYInd, relativeXInd_, tetrominoMesh_);

    return canPlace;
  }

  /**
   * Return true if it's possible to move the tetromino to the left in the backgroundMesh
   *
   * \return true if it's possible to move the tetromino to the left
   */
  method bool canMoveLeft(){
    var int newRelativeXInd;
    var bool canPlace;

    let newRelativeXInd = relativeXInd_ - 1;
    let canPlace = canBePlaced(relativeYInd_, newRelativeXInd, tetrominoMesh_);
    return canPlace;
  }

  /**
   * Return true if it's possible to move the tetromino to the right in the backgroundMesh
   *
   * \return true if it's possible to move the tetromino to the right
   */
  method bool canMoveRight(){
    var int newRelativeXInd;
    var bool canPlace;

    let newRelativeXInd = relativeXInd_ + 1;
    let canPlace = canBePlaced(relativeYInd_, newRelativeXInd, tetrominoMesh_);
    return canPlace;
  }

  /** Move the tetromino down in the background mesh */
  method void moveDown(){
    // Erase the previous position
    do background_.drawMesh(true, false, tetrominoMesh_, relativeYInd_, relativeXInd_);
    let relativeYInd_ = relativeYInd_ + 1;
    // Draw
    do background_.drawMesh(false, false, tetrominoMesh_, relativeYInd_, relativeXInd_);
    return;
  }

  /** Move the tetromino to the left in the background mesh */
  method void moveLeft(){
    // Erase the previous position
    do background_.drawMesh(true, false, tetrominoMesh_, relativeYInd_, relativeXInd_);
    let relativeXInd_ = relativeXInd_ - 1;
    // Draw
    do background_.drawMesh(false, false, tetrominoMesh_, relativeYInd_, relativeXInd_);
    return;
  }

  /** Move the tetromino to the right in the background mesh */
  method void moveRight(){
    // Erase the previous position
    do background_.drawMesh(true, false, tetrominoMesh_, relativeYInd_, relativeXInd_);
    let relativeXInd_ = relativeXInd_ + 1;
    // Draw
    do background_.drawMesh(false, false, tetrominoMesh_, relativeYInd_, relativeXInd_);
    return;
  }

}
