/**
 * Implements the Tetromino class
 */

/**
 * Class which deals with the tetrominos in the following way
 * - Movement (side-ways, down, rotate)
 * - Checks if it can be placed in another mesh without colliding with elements
 *   or be out of bounds
 * - Draw and erase the tetromino within another mesh
 */
class Tetromino {
  field int relativeXInd_;  // x-coordinate in the background mesh (i.e. not the tetromino mesh coordinate) relative to top left corner of relativeMesh
  field int relativeYInd_;  // y-coordinate in the background mesh (i.e. not the tetromino mesh coordinate) relative to top left corner of relativeMesh

  field Mesh tetrominoMesh_;  // The mesh of the tetromino itself
  field Background background_;  // The background to interact with

  field String tetrominoName;  // Name of the tetromino

  static int allocs;  // Number of times this has been allocated

  /**
   * Construct the tetromino
   *
   * \param tetrominoMesh The mesh of the tetromino itself
   * \param relativeYInd y-coordinate in the background mesh (i.e. not the tetromino mesh coordinate) relative to top left corner of relativeMesh
   * \param relativeXInd x-coordinate in the background mesh (i.e. not the tetromino mesh coordinate) relative to top left corner of relativeMesh
   * \param background The background to interact with
   * \param int The type of tetromino (0-I, 1-J, 2-L, 3-O, 4-S, 5-T, 6-Z)
   */
  constructor Tetromino new(Mesh tetrominoMesh,
                            int relativeYInd,
                            int relativeXInd,
                            Background background,
                            int type) {
    let tetrominoMesh_ = tetrominoMesh;

    let relativeXInd_ = relativeXInd;
    let relativeYInd_ = relativeYInd;

    let background_ = background;

    let allocs = allocs + 1;

    if(type = 0){ let tetrominoName = "I"; }
    if(type = 1){ let tetrominoName = "J"; }
    if(type = 2){ let tetrominoName = "L"; }
    if(type = 3){ let tetrominoName = "O"; }
    if(type = 4){ let tetrominoName = "S"; }
    if(type = 5){ let tetrominoName = "T"; }
    if(type = 6){ let tetrominoName = "Z"; }

    return this;
  }

  /**
   * Destroy the Tetromino
   *
   * Disposes:
   * - The tetrominoMesh
   * - The tetrominoName
   * - This
   */
  method void dispose (){
    let allocs = allocs - 1;

    do tetrominoMesh_.dispose();
    do tetrominoName.dispose();
    do Memory.deAlloc(this);
    return;
  }

  /** Print the number of allocs */
  method void printAlloc (){
    var String str;
    let str = "Tetromino allocs: ";
    do Output.printString(str);
    do Output.printInt(allocs);
    do str.dispose();
    return;
  }

  /** Return the tetrominoMesh_ */
  method int getTetrominoMesh(){
    return tetrominoMesh_;
  }

  /** Return relativeXInd_ */
  method int getRelativeIndX(){
    return relativeXInd_;
  }

  /** Return relativeYInd_ */
  method int getRelativeIndY(){
    return relativeYInd_;
  }
  /** Return relativeYInd_ */
  method String getTetrominoName(){
    return tetrominoName;
  }

  /**
   * Set the background
   *
   * \param background The new background
   */
  method void setBackground(Background background){
    let background_ = background;
    return;
  }

  /**
   * Set the relative position
   *
   * \param yInd The relative y index
   * \param xInd The relative x index
   */
  method void setRelativePosition(int yInd, int xInd){
    let relativeYInd_ = yInd;
    let relativeXInd_ = xInd;
    return;
  }

  /**
   * Rotate the tetromino clockwise
   */
  method void rotate(){
    return;
  }

  /**
   * Return true if it's possible to spawn the tetromino in the gameMesh
   *
   * \return true if it's possible to spawn the tetromino
   */
  method bool canSpawn(){
    var bool canBeSpawn;

    let canBeSpawn = canBePlaced(relativeYInd_, relativeXInd_);

    if(canBeSpawn){
      do background_.drawMesh(false, tetrominoMesh_, relativeYInd_, relativeXInd_);
      return true;
    } else{
      return false;
    }
  }

  /**
   * Return true if it's possible to place the tetromino with the new coordinates in the gameMesh
   *
   * \param newRelativeYInd The new y-coordinate in the gameMesh
   * \param newRelativeXInd The new x-coordinate in the gameMesh
   * \return true if it's possible to place the tetromino
   */
  method bool canBePlaced(int newRelativeYInd, int newRelativeXInd){
    var int tetrominoMeshIndex;
    var int tetrominoXIndex;
    var int tetrominoYIndex;
    var int gameXInd;
    var int gameYInd;

    var Array tetrominoIndices;  // Will be destroyed by the Mesh
    var Mesh gameMesh;

    let gameMesh = background_.getBackgroundMesh();

    while(tetrominoMeshIndex < tetrominoMesh_.getMeshLength()){
      let tetrominoIndices = tetrominoMesh_.getMeshIndices(tetrominoMeshIndex);
      let tetrominoXIndex = tetrominoIndices[0];
      let tetrominoYIndex = tetrominoIndices[1];

      let gameXInd = newRelativeXInd + tetrominoXIndex;
      let gameYInd = newRelativeYInd + tetrominoYIndex;

      // Check that x is within limits
      if (gameXInd < 0){ return false; }
      // -1 as gameXInd counts from 0
      if (gameXInd > (gameMesh.getCols() - 1)){ return false; }

      // Check that y is within limits
      if (gameYInd < 0){ return false; }
      // -1 as gameYInd counts from 0
      if (gameYInd > (gameMesh.getRows() - 1)){ return false; }

      // Check whether we would collide
      if (gameMesh.getState(gameYInd, gameXInd)){ return false; }
      let tetrominoMeshIndex = tetrominoMeshIndex + 1;
    }

    return true;
  }

  /**
   * Return true if it's possible to move the tetromino down in the gameMesh
   *
   * \return true if it's possible to move the tetromino down
   */
  method bool canMoveDown(){
    var int newRelativeYInd;
    var bool canPlace;

    let newRelativeYInd = relativeYInd_ + 1;
    let canPlace = canBePlaced(newRelativeYInd, relativeXInd_);

    return canPlace;
  }

  /**
   * Return true if it's possible to move the tetromino to the left in the gameMesh
   *
   * \return true if it's possible to move the tetromino to the left
   */
  method bool canMoveLeft(){
    var int newRelativeXInd;
    var bool canPlace;

    let newRelativeXInd = relativeXInd_ - 1;
    let canPlace = canBePlaced(relativeYInd_, newRelativeXInd);
    return canPlace;
  }

  /**
   * Return true if it's possible to move the tetromino to the right in the gameMesh
   *
   * \return true if it's possible to move the tetromino to the right
   */
  method bool canMoveRight(){
    var int newRelativeXInd;
    var bool canPlace;

    let newRelativeXInd = relativeXInd_ + 1;
    let canPlace = canBePlaced(relativeYInd_, newRelativeXInd);
    return canPlace;
  }

  /** Move the tetromino down in the game mesh */
  method void moveDown(){
    // Erase
    do background_.drawMesh(true, tetrominoMesh_, relativeYInd_, relativeXInd_);
    let relativeYInd_ = relativeYInd_ + 1;
    // Draw
    do background_.drawMesh(false, tetrominoMesh_, relativeYInd_, relativeXInd_);
    return;
  }

  /** Move the tetromino to the left in the game mesh */
  method void moveLeft(){
    // Erase
    do background_.drawMesh(true, tetrominoMesh_, relativeYInd_, relativeXInd_);
    let relativeXInd_ = relativeXInd_ - 1;
    // Draw
    do background_.drawMesh(false, tetrominoMesh_, relativeYInd_, relativeXInd_);
    return;
  }

  /** Move the tetromino to the right in the game mesh */
  method void moveRight(){
    // Erase
    do background_.drawMesh(true, tetrominoMesh_, relativeYInd_, relativeXInd_);
    let relativeXInd_ = relativeXInd_ + 1;
    // Draw
    do background_.drawMesh(false, tetrominoMesh_, relativeYInd_, relativeXInd_);
    return;
  }

}
